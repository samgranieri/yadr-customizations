prompt_ashmckenzie_directory () {
  _prompt_ashmckenzie_directory="%F{yellow}%~%f"
}

prompt_ashmckenzie_who_and_where () {
  _prompt_ashmckenzie_who_and_where="%F{green}%n@%F{cyan}%m%f"
}

prompt_ashmckenzie_precmd () {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_ashmckenzie_who_and_where
  prompt_ashmckenzie_directory

  if (( $+functions[git-info] ))
  then
    git-info
  fi

  if (( $+functions[ruby-info] ))
  then
    ruby-info
  fi
}

prompt_ashmckenzie_setup () {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)
  autoload -Uz add-zsh-hook
  add-zsh-hook precmd prompt_ashmckenzie_precmd

  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{red}❯%F{yellow}❯%F{green}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'

  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format '%%B%F{green}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format '%%B%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format '%%B%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format ':%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format '%%B%F{red}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format '%%B%F{blue}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format '%%B%F{magenta}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format '%%B%F{cyan}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format '%%B%F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format '%%B%F{white}◼%f%%b'
  zstyle ':prezto:module:git:info:clean' format '%F{green︎}✔%f'

  zstyle ':prezto:module:git:info:keys' format 'prompt' '[%C%A%B%S%a%d%m%r%U%u%b]-'

  zstyle ':prezto:module:ruby:info:version' format '%v%f'

  PROMPT=$'\n[${_prompt_ashmckenzie_who_and_where}]%f-[%*]-[${ruby_info[version]}]-${git_info[prompt]}[${_prompt_ashmckenzie_directory}]\n$ '
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_ashmckenzie_setup "$@"