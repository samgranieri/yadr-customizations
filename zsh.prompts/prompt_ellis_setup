#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Ellis Whitehead
#
# Screenshots:
#

# Load dependencies.
pmodload 'helper'

start_time=$SECONDS
function prompt_ellis_preexec {
  start_time=$SECONDS
}

# Copied from http://www.paradox.gd/posts/9-my-new-zsh-prompt
function calc_elapsed_time {
  if [[ $timer_result -ge 3600 ]]; then
    let "timer_hours = $timer_result / 3600"
    let "remainder = $timer_result % 3600"
    let "timer_minutes = $remainder / 60"
    let "timer_seconds = $remainder % 60"
    print -P "%B%F{red}>>> elapsed time ${timer_hours}h${timer_minutes}m${timer_seconds}s%b"
  elif [[ $timer_result -ge 60 ]]; then
    let "timer_minutes = $timer_result / 60"
    let "timer_seconds = $timer_result % 60"
    print -P "%B%F{yellow}>>> elapsed time ${timer_minutes}m${timer_seconds}s%b"
  elif [[ $timer_result -gt 10 ]]; then
    print -P "%B%F{green}>>> elapsed time ${timer_result}s%b"
  fi
}

function prompt_ellis_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Update timer
  if [ -n "${start_time+x}" ]; then
    timer_result=$(($SECONDS-$start_time))
    if [[ $timer_result -gt 10 ]]; then
      calc_elapsed_time
    fi
    unset start_time
  fi
}

function prompt_ellis_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook preexec prompt_ellis_preexec
  add-zsh-hook precmd prompt_ellis_precmd

  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%F{blue}❯%f'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '%F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{blue}❮%f'
  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%F{yellow}%s%f'
  zstyle ':prezto:module:git:info:added' format ' %F{green}✚%f'
  zstyle ':prezto:module:git:info:ahead' format ' %F{yellow}⬆%f'
  zstyle ':prezto:module:git:info:behind' format ' %F{yellow}⬇%f'
  zstyle ':prezto:module:git:info:branch' format ':%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %F{red}✖%f'
  zstyle ':prezto:module:git:info:modified' format ' %F{blue}✱%f'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %F{magenta}➜%f'
  zstyle ':prezto:module:git:info:stashed' format ' %F{cyan}✭%f'
  zstyle ':prezto:module:git:info:unmerged' format ' %F{yellow}═%f'
  zstyle ':prezto:module:git:info:untracked' format ' %F{white}◼%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '%K{black} (%F{blue}git%f$(coalesce "%b" "%p" "%c")%s%A%B%S%a%d%m%r%U%u) %k'

  ssh_prompt="%F{green}%n%f@%F{blue}%m%f "
  # Define prompts.
  PROMPT='
%K{blue}%B%F{white}%~ %f%b%k%K{black} !%h %k${git_info:+${(e)git_info[prompt]}}%(!. %B%F{red}#%f%b.)
${SSH_CONNECTION+${ssh_prompt}}${editor_info[keymap]} %F{yellow}%#%f '
  RPROMPT='${editor_info[overwrite]}%(?:: %F{red}⏎%f)${VIM:+" %B%F{green}V%f%b"}%*'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
# colors
#%F{white}X%F{black}X%F{red}X%F{green}X%F{blue}X%F{yellow}X%F{magenta}X%F{cyan}X%f
#%B%F{white}X%F{black}X%F{red}X%F{green}X%F{blue}X%F{yellow}X%F{magenta}X%F{cyan}X%f%b
#%K{white}X%K{black}X%K{red}X%K{green}X%K{blue}X%K{yellow}X%K{magenta}X%K{cyan}X%k
}

prompt_ellis_setup "$@"
